plugins {
    id 'fabric-loom' version '0.2.7-SNAPSHOT'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.8.4'
    id 'net.minecrell.licenser' version '0.4.1'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

if (isNullOrEmpty(System.getenv("TRAVIS_TAG")) && !isNullOrEmpty(System.getenv('TRAVIS_BUILD_NUMBER'))) {
    version = project.modVersion + '.' + System.getenv('TRAVIS_BUILD_NUMBER')
} else {
    version = project.modVersion
}
logger.lifecycle("${project.repoName} version ${project.version}")
group = project.mavenGroup

repositories {
    mavenCentral()
}

minecraft {
}

dependencies {
    //to change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraftVersion}"
    mappings "net.fabricmc:yarn:${project.yarnMappings}:v2"
    modCompile "net.fabricmc:fabric-loader:${project.loaderVersion}"

    modCompile "net.fabricmc.fabric-api:fabric-api-base:0.1.3+12a8474cad"
}

license {
    header = file('HEADER.txt')
    include '**/*.java'
    style.java = 'BLOCK_COMMENT'
    newLine = false
}

// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

tasks.withType(Javadoc) {
    options {
        encoding = 'UTF-8'
        charSet = 'UTF-8'
        memberLevel = JavadocMemberLevel.PACKAGE
        // Disable the crazy super-strict doclint tool in Java 8
        addStringOption('Xdoclint:none', '-quiet')
    }
}

def outputDirectory = file("$buildDir/pom-properties")
def pomProperties = file("$outputDirectory/pom.properties")

task generatePomPropertiesForJar {
    outputDirectory.mkdirs()
    pomProperties.text = """# Generated by Gradle ${gradle.gradleVersion} on ${new Date()}
groupId=${project.group}
artifactId=${project.name}
version=${project.version}
"""
    outputs.file(pomProperties)
}

processResources {
    inputs.properties "version": project.version, "description": project.description

    from(sourceSets.main.resources.srcDirs) {
        include "fabric.mod.json"
        include "pack.mcmeta"
        expand(
                "version": project.version,
                "description": project.description,
                "repoOwner": project.repoOwner,
                "repoName": project.repoName,
                "id": project.modId,
                "minecraftVersionLimit": project.minecraftVersionLimit,
                "loaderVersionLimit": project.loaderVersionLimit
        )
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude "fabric.mod.json"
        exclude "pack.mcmeta"
    }
}

// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
// if it is present.
// If you remove this task, sources will not be generated.
task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set "sources"
    from sourceSets.main.allSource
    from "LICENSE"
}

javadoc {
    source = sourceSets.main.allJava
    classpath = sourceSets.main.compileClasspath + sourceSets.main.output

    options {
        source = "8"
        encoding = 'UTF-8'
        charSet = 'UTF-8'
        memberLevel = JavadocMemberLevel.PACKAGE
        if (file("readme.html").exists()) {
            overview = "readme.html"
        }
        links(
                'https://guava.dev/releases/21.0/api/docs/',
                'https://asm.ow2.io/javadoc/',
                'https://docs.oracle.com/javase/8/docs/api/',
                'https://logging.apache.org/log4j/2.x/log4j-api/apidocs/',
                'https://netty.io/4.1/api/',
                "https://maven.fabricmc.net/docs/fabric-api-${project.apiVersion}/",
                "https://maven.fabricmc.net/docs/yarn-${project.yarnMappings}/"
                // Need to add minecraft jd publication once there is one available
        )
    }
    include("**/api/**")
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set "javadoc"
    from javadoc.destinationDir
}

jar {
    from "LICENSE"
}

afterEvaluate {
    artifacts {
        archives remapJar
        archives(sourcesJar) {
            builtBy remapSourcesJar
        } // wait for loom pr
        archives javadocJar
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.getProperty('bintrayUser') : System.getenv('bintray_user')
    key = project.hasProperty('bintrayKey') ? project.getProperty('bintrayKey') : System.getenv('bintray_key')
    publish = true
    override = true
    publications = ['mavenJava']
    pkg {
        repo = project.bintrayRepo
        name = project.name
        user = bintray.user
        userOrg = "liachmodded"
        licenses = [project.codeLicense]
        desc = project.description
        websiteUrl = "https://github.com/${project.repoOwner}/${project.repoName}"
        issueTrackerUrl = "https://github.com/${project.repoOwner}/${project.repoName}/issues"
        vcsUrl = "https://github.com/${project.repoOwner}/${project.repoName}.git"
        publicDownloadNumbers = true

        githubRepo = "${project.repoOwner}/${project.repoName}"
        version {
            name = project.version
            released = new Date()
            if (!isNullOrEmpty(System.getenv("TRAVIS_TAG"))) {
                vcsTag = System.getenv("TRAVIS_TAG")
            } else {
                vcsTag = ''
            }
        }
    }
}

// configure the maven publication
publishing {
    publications {
        mavenJava(MavenPublication) {
            pom {
                name = project.name
                description = project.description
                url = "https://github.com/${project.repoOwner}/${project.repoName}"
                licenses {
                    license {
                        name = project.codeLicense
                    }
                }

                scm {
                    url = "https://github.com/${project.repoOwner}/${project.repoName}"
                }
            }

            // add all the jars that should be included when publishing to maven
            afterEvaluate {
                artifact remapJar
                artifact(sourcesJar) {
                    builtBy remapSourcesJar
                }
                // artifact remapSourcesJar wait for loom pr
                artifact javadocJar
            }
        }
    }

    if (!isNullOrEmpty(System.getenv('GITHUB_OAUTH_TOKEN'))) {
        publications {
            mavenGitHub(MavenPublication) {
                pom {
                    name = project.name
                    description = project.description
                    url = "https://github.com/${project.repoOwner}/${project.repoName}"
                    licenses {
                        license {
                            name = project.codeLicense
                        }
                    }

                    scm {
                        url = "https://github.com/${project.repoOwner}/${project.repoName}"
                    }
                }

                // add all the jars that should be included when publishing to maven
                artifact remapJar
            }
        }
        // select the repositories you want to publish to
        repositories {
            maven {
                name = "GitHub"
                url = uri("https://maven.pkg.github.com/${project.repoOwner}/${project.repoName}")
                credentials {
                    username = System.getenv('GITHUB_USERNAME')
                    password = System.getenv('GITHUB_OAUTH_TOKEN')
                }
            }
        }
    }
}

tasks.withType(Jar) {
    dependsOn("generatePomFileForMavenJavaPublication", "generatePomPropertiesForJar")
    into("META-INF/maven/${project.group}/${project.name}") {
        from(generatePomFileForMavenJavaPublication) {
            rename(".+", "pom.xml")
        }
        from(generatePomPropertiesForJar)
    }
}

static boolean isNullOrEmpty(String st) {
    return st == null || st.isEmpty()
}

runClient {
    jvmArgs "-Dlog4j.configurationFile=../logConfig.xml", "-Dmixin.debug=true"
}

runServer {
    jvmArgs "-Dlog4j.configurationFile=../logConfig.xml", "-Dmixin.debug=true"
}
